# -*- coding: utf-8 -*- #

PR include "../types/task.a68" PR
PR include "../globals.a68" PR;
PR include "../file.a68" PR;

PROC add task = (REF INT argi) VOID: (
	TASK task := (
		# id # 0,
		# title # "",
		# description # "",
		# reason # NIL,
		# state # pending,
		# date # today date,
		# time # (NIL, NIL),
		# repeatition # today stack
	);

	BEGIN
		FILE f;

		openf(f, "./last-id.txt", stand in channel);
		get(f, id OF task);
		close(f);

		id OF task +:= 1;

		openf(f, "./last-id.txt", stand out channel);
		put(f, id OF task);
		close(f)
	END;

	title OF task := get argv(argi +:= 1, "Expected task title");
	description OF task := (a68g argc >= argi + 1 | a68g argv(argi +:= 1) | "");

	WHILE argi < a68g argc DO
		STRING arg := a68g argv(argi +:= 1);

		IF arg = "at" THEN
			arg := get argv(argi +:= 1, "Expected time after 'at'");
			time OF task := (LOC TIME := str to time(arg), NIL)
		ELIF arg = "to" THEN
			arg := get argv(argi +:= 1, "Expected time after 'to'");
			finish OF time OF task := LOC TIME := str to time(arg)
		ELSE
			putf(stand error, ($g"[31mUnexpected '"g"' in here"g"[m"ll$, esc, arg, esc));
			stop
		FI
	OD;

	append file("./tasks.csv", task to csv(task))
)
