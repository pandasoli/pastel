# -*- coding: utf-8 -*- #

PR include "./types/task/remove.a68" PR;
PR include "./types/note/remove.a68" PR;
PR include "./types/week/remove.a68" PR
PR include "../globals.a68" PR
PR include "../quick sort.a68" PR;

PROC remove = (REF INT argi) VOID: (
	STRING arg := get argv(argi +:= 1, "Expected index or 'week' after 'remove'");

	IF arg = "week" THEN
		process week remove(arg, argi);
		stop
	FI;

	INT index = TOINT arg;

	[] ITEM items = get today items;

	IF index > UPB items THEN
		putf(stand error, ($g"[31m"gg"[m"ll$, esc, "There're not many items in today's list", esc));
		stop
	FI;

	ITEM item = items[index];
	INT id = get item id(item);

	# Remove from items list #
	CASE item IN
		(NOTE note): process note remove(id OF note),
		(TASK task): process task remove(id OF task)
	ESAC;

	# Remove from group #
	[] GROUP groups = get today groups;
	REF GROUP group := NIL;

	FOR i TO UPB groups DO
		IF includes(ids OF groups[i], id) THEN
			group := LOC GROUP := groups[i];
			finish groups
		FI
	OD;
	finish groups:

	IF group :/=: REF GROUP(NIL) THEN
		# Remove id from group #
		[] INT ids = ids OF group;
		ids OF group := ~;

		FOR i TO UPB ids DO
			IF ids[i] /= id THEN
				ids OF group +:= ids[i]
			FI
		OD;

		# Store new group #
		append file("./new.csv", group csv header);

		get all groups((GROUP g) VOID:
			IF id OF g /= id OF group THEN
				append file("./new.csv", group to csv(g))
			ELIF UPB ids OF group > 0 THEN
				append file("./new.csv", group to csv(group))
			FI
		);

		move file("./new.csv", groups data path)
	FI
)
