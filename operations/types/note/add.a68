# -*- coding: utf-8 -*- #

PROC process note add = (REF STRING arg, REF INT argi, INT id) NOTE: (
	NOTE note := (
		# id # id,
		# content # "",
		# date # today date,
		# time # NIL
	);

	content OF note := get argv(argi +:= 1, "Expected note's content after '" + arg + "'");

	WHILE argi < a68g argc DO
		arg := a68g argv(argi +:= 1);

		IF arg = "date" THEN
			arg := get argv(argi +:= 1, "Expected date after 'date'");
			date OF note := get week start of day(str to date(arg))
		ELIF arg = "at" THEN
			arg := get argv(argi +:= 1, "Expected time after 'at'");
			time OF note := LOC TIME := str to time(arg)
		ELSE
			putf(stand error, ($g"[31mUnexpected '"g"'"g"[m"ll$, esc, arg, esc));
			stop
		FI
	OD;

	note
)
