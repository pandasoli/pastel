# -*- coding: utf-8 -*- #

PR include "paths.a68" PR
PR include "utils.a68" PR
PR include "../types/todo.a68" PR;

PROC get todos = (PROC (TODO) VOID callback) VOID:
	read db(path todos, (CSVFIELDS fields) VOID:
		callback(csv to todo(fields))
	);

PROC get all todos = [] TODO: (
	FLEX [0] TODO todos;

	read db(path todos, (CSVFIELDS fields) VOID:
		todos +:= csv to todo(fields));

	todos
);

PROC write todo = (TODO todo) VOID:
	append file(path todos, todo to csv(todo));

PROC write todos = ([] TODO todos) VOID: (
	FILE f;

	IF open(f, path todos, stand out channel) > 0 THEN
		putf(stand error, ($"Couldn't open "gl$, path todos));
		stop
	FI;

	putf(f, ($gl$, todo csv header));

	FOR i TO UPB todos DO
		putf(f, ($gl$, todo to csv(todos[i])))
	OD;

	close(f)
)
