# -*- coding: utf-8 -*- #

PR include "../csv.a68" PR;

STRING todo csv header = "ID,TITLE,DESCRIPTION";

MODE TODO = STRUCT (
	INT id,
	STRING title,
	STRING desc
);

OP +:= = (REF FLEX [] TODO in out, TODO item) VOID: (
	[UPB in out + 1] TODO new;
	new[:UPB in out] := in out;
	new[UPB new] := item;
	in out := new
);

PROC csv to todo = (FLEXCSVFIELDS fields) TODO:
	TODO(
		# id # TOINT fields[1],
		# title # fields[2],
		# description # fields[3]
	);

PROC todo to csv = (TODO todo) STRING: (
	[] STRING fields = (
		# id # whole(id OF todo, 0),
		# title # """" + title OF todo + """",
		# description # """" + desc OF todo + """"
	);

	join(fields, ",")
)
