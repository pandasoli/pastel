# -*- coding: utf-8 -*- #

STRING group csv header = "ID,TITLE,DATE,IDS";

MODE GROUP = STRUCT (
	INT id,
	STRING title,
	DATE date,
	FLEX [0] INT ids
);

OP +:= = (REF FLEX [] GROUP in out, GROUP item) VOID: (
	[UPB in out + 1] GROUP new;
	new[:UPB in out] := in out;
	new[UPB new] := item;
	in out := new
);

PROC csv to group = (FLEXCSVFIELDS fields) GROUP: (
	PROC split ids = (STRING raw ids) [] INT: (
		FLEX [0] INT ids;
		STRING id;

		FOR i TO UPB raw ids DO
			IF raw ids[i] = ":" THEN
				ids +:= TOINT id;
				id := ""
			ELSE
				id +:= raw ids[i]
			FI
		OD;

		ids +:= TOINT id;
		ids
	);

	GROUP(
		# id # TOINT fields[1],
		# title # fields[2],
		# date # str to date(fields[3]),
		# ids # split ids(fields[4])
	)
);

PROC group to csv = (GROUP group) STRING: (
	STRING ids;

	FOR i TO UPB ids OF group DO
		ids +:= whole((ids OF group)[i], 0) + (i < UPB ids OF group | ":" | "")
	OD;

	[] STRING fields = (
		# id # whole(id OF group, 0),
		# title # """" + title OF group + """",
		# date # REPR date OF group,
		# ids # ids
	);

	join(fields, ",")
)
