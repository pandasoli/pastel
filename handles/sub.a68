# -*- coding: utf-8 -*- #

PR include "../database.a68" PR
PR include "../database/task.a68" PR
PR include "../database/lastid.a68" PR;

PR include "add.a68" PR;
PR include "rm.a68" PR;
PR include "done.a68" PR;
PR include "sort.a68" PR;
PR include "cal.a68" PR

PR include "note/sub.a68" PR
PR include "todo/sub.a68" PR;

PROC main sub = (REF INT argi) VOID: (
	STRING arg := a68g argv(argi);
	argi +:= 1;

	PROC print tasks = (DATE date) VOID: (
		INT i := 1;

		get tasks((TASK task) VOID:
			IF date OF task = date THEN
				printf(($g"[1m"g(0)g"[m "$, esc, i, esc));

				IF state OF task = task_done
				THEN printf(($g"[9m"gg"[m"l$, esc, title OF task, esc))
				ELSE printf(($gl$, title OF task))
				FI;

				IF desc OF task /= "" THEN
					[] STRING lines = split text(desc OF task, REPR 10);

					FOR j TO UPB lines DO
						[] STRING parts = wrap text(lines[j], 60);
						STRING part = join(parts, REPR 10 + "  ");
						printf(($"  "gl$, part))
					OD
				FI;

				i +:= 1
			FI
		)
	);

	IF arg = "" THEN print tasks(today)
	ELIF arg = "add"  OR arg = "a" THEN add(argi)
	ELIF arg = "note" OR arg = "n" THEN sub note(argi)
	ELIF arg = "todo" OR arg = "t" THEN sub todo(argi)
	ELIF arg = "rm"   OR arg = "r" THEN rm(argi)
	ELIF arg = "done" OR arg = "d" THEN done(argi)
	ELIF arg = "sort" OR arg = "s" THEN sort(argi)
	ELIF arg = "cal"  OR arg = "c" THEN cal(argi)
	ELIF arg = "H"                 THEN show help page("pastel long")
	ELSE
		INT n = TOINT arg;

		IF n /= 0
		THEN print tasks(today + n)
		ELSE show help page("pastel")
		FI
	FI
)
