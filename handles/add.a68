# -*- coding: utf-8 -*- #

PR include "../database/task.a68" PR
PR include "../database/lastid.a68" PR;

#
	add task <<
	add task description ... todo
#
PROC add = (REF INT argi) VOID:
	WHILE argi <= a68g argc DO
		STRING arg = a68g argv(argi);
		argi +:= 1;

		TASK task := (
			# id # inc id,
			# title # arg,
			# description # "",
			# state # task_pending,
			# date # today
		);

		IF argi <= a68g argc
		THEN desc OF task := a68g argv(argi); argi +:= 1
		ELSE
			on logical file end(stand in, (REF FILE f) BOOL: done in);

			getf(stand in, ($gl$, desc OF task));
			IF desc OF task = "" THEN done in FI;

			DO STRING in;
				getf(stand in, ($gl$, in));
				IF in = "" THEN done in FI;
				desc OF task +:= REPR 10 + in
			OD;

			done in: ~
		FI;

		write task(task)
	OD
