# -*- coding: utf-8 -*- #

PR include "../../database/note.a68" PR;

PR include "utils.a68" PR;

PR include "add.a68" PR;
PR include "rm.a68" PR;

# note [add|rm|groups|[group]] #
PROC sub note = (REF INT argi) VOID: (
	PROC print notes = ([] NOTE notes) VOID:
		FOR i TO UPB notes DO
			printf(($gg(0)g" "$, cl one, i, cl clear));

			[] STRING lines = split text(content OF notes[i], REPR 10);

			IF UPB lines = 1
			THEN printf(($gl$, lines[1]))
			ELSE
				new line(stand out);
				FOR j TO UPB lines
				DO printf(($"  "gl$, lines[j])) OD
			FI;

			IF i < UPB notes THEN new line(stand out) FI
		OD;

	IF argi <= a68g argc THEN
		STRING arg = a68g argv(argi);
		argi +:= 1;

		IF   arg = "add"    OR arg = "a" THEN note add(argi)
		ELIF arg = "rm"     OR arg = "r" THEN note rm(argi)
		ELIF arg = "H"                   THEN show help page("note")
		ELIF arg = "groups" OR arg = "g" THEN
			[] STRING groups = get note groups;

			FOR j TO UPB groups
			DO printf(($gl$, groups[j]))
			OD
		ELSE print notes(get group notes(arg))
		FI
	ELSE
		[] STRING groups = get note groups;

		FOR j TO UPB groups
		DO printf(($g"["g"]"gl$, cl one, groups[j], cl clear))
		OD;

		new line(stand out);

		print notes(get global notes)
	FI
)
