PR include "types.a68" PR;

PROC sort tasks = (TASK a, b) BOOL: (
	BOOL
		a all day = time OF a :=: REF TIMES(NIL),
		b all day = time OF b :=: REF TIMES(NIL);

		IF a all day THEN TRUE # a #
	ELIF b all day THEN FALSE # b #
	ELSE
		INT
			a hours   = hours   OF start OF time OF a,
			b hours   = hours   OF start OF time OF b,
			a minutes = minutes OF start OF time OF a,
			b minutes = minutes OF start OF time OF b;

		IF a hours = b hours THEN
			IF a minutes < b minutes THEN TRUE # a #
			ELSE FALSE # b #
			FI
		ELIF a hours < b hours THEN TRUE # a #
		ELSE FALSE # b #
		FI
	FI
);

PROC swap = (REF [] TASK arr, INT i, j) VOID: (
	TASK temp = arr[i];
	arr[i] := arr[j];
	arr[j] := temp
);

PROC partition = (REF [] TASK arr, INT low, high) INT: (
	TASK pivot = arr[high];
	INT i := low - 1;

	FOR j FROM low TO high - 1 DO
		IF sort tasks(arr[j], pivot) THEN
			i +:= 1;
			swap(arr, i, j)
		FI
	OD;

  swap(arr, i + 1, high);
  i + 1
);

PROC quick sort tasks = (REF [] TASK arr, INT low, high) VOID:
	IF low < high THEN
		INT pi = partition(arr, low, high);

		quick sort tasks(arr, low, pi - 1);
		quick sort tasks(arr, pi + 1, high)
	FI
