#!/usr/bin/a68g --script #
# -*- coding: utf-8 -*- #

# vim:ft=algol68
#

PR include "types/todo.a68" PR
PR include "database.a68" PR;
PR include "handles/add.a68" PR
PR include "handles/rm.a68" PR;
PR include "handles/done.a68" PR
PR include "handles/sort.a68" PR;
PR include "unix.a68" PR;
PR include "utils.a68" PR;

BEGIN
	PROC a = (STRING path, header) VOID:
		IF NOT file is regular(path)
		THEN FILE f;
			IF establish(f, path, stand out channel) < 0 THEN
				putf(stand error, ($"Couldn't establish "gl$, path));
				stop
			FI;

			put(f, header + REPR 10);
			close(f)
		FI;

	IF NOT file is directory(data path)
	THEN create directory(data path)
	FI;

	a(todos path, todo csv header);
	a(last id path, "0")
END;

IF a68g argc = 1 THEN
	[] TODO todos = get all today todos;

	FOR i TO UPB todos DO
		printf(($g"[1m"g(0)g"[m "$, esc, i, esc));

		IF state OF todos[i] = todo_done
		THEN printf(($g"[9m"gg"[m"l$, esc, title OF todos[i], esc))
		ELSE printf(($gl$, title OF todos[i]))
		FI
	OD
ELSE
	INT argi := 2;
	STRING arg = a68g argv(argi); argi +:= 1;

	IF   arg = "add"  THEN add(argi)
	ELIF arg = "rm"   THEN rm(argi)
	ELIF arg = "done" THEN done(argi)
	ELIF arg = "sort" THEN sort(argi)
	FI
FI
